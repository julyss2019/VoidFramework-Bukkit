plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
}

subprojects {
    version '2.4.1-SNAPSHOT'
    group 'com.void01.bukkit'

    kotlin {
        jvmToolchain(8)
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'

        options.encoding = 'UTF-8'
        options.compilerArgs.add('-parameters')
        options.compilerArgs.add('-XDenableSunApiLintControl') // 抑制内部 API 警告
    }

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            url 'https://maven.void01.com/repository/bukkit-plugin/'
        }
    }

    publishing {
        repositories {
            maven {
                if (version.toString().endsWith("-SNAPSHOT")) {
                    url = 'https://maven.void01.com/repository/minecraft-snapshots/'
                } else {
                    url = 'https://maven.void01.com/repository/minecraft-releases/'
                }

                credentials {
                    username = project.findProperty('nexus.username')
                    password = project.findProperty('nexus.password')
                }
            }
        }
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok:1.18.28'
        compileOnly 'org.projectlombok:lombok:1.18.28'

        compileOnly 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        compileOnly fileTree(dir: new File(projectDir, 'libs').getAbsolutePath(), includes: ['*jar'])

        compileOnly 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
        compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
        compileOnly 'net.md-5:bungeecord-chat:1.16-R0.4'

        testImplementation 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
        testImplementation 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
        testImplementation("org.mockito:mockito-core:4.11.0")
    }

    test {
        useJUnitPlatform()
    }

    getProject().tasks.named('build') {
        dependsOn publishToMavenLocal
    }

    java {
        withSourcesJar()
    }

    javadoc {
        options.encoding 'UTF-8'
        options.jFlags "-Dfile.encoding=UTF-8"
    }
}